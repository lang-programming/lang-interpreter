lang.version = v1.0.0
lang.name = Lang Standard Implementation (&Complex)

class &Complex {
    +final:$real{DOUBLE}
    +final:$imag{DOUBLE}

    +construct = ($real{number}, $imag{number}):{VOID} -> {
        ::$real $= $LANG_TYPE_DOUBLE($real)
        ::$imag $= $LANG_TYPE_DOUBLE($imag)
    }

    +mp.conjugate = ():{OBJECT} -> {
        return parser.op(&This(::$real, -::$imag))
    }

    +mp.arg = ():{DOUBLE} -> {
        return parser.op(atan2(::$imag, ::$real))
    }

    +op:deepCopy = ():{OBJECT} -> {
        return parser.op(&This(::$real, ::$imag))
    }

    +op:inv = ():{OBJECT} -> {
        return parser.op(&This(-::$real, -::$imag))
    }

    +op:abs = ():{DOUBLE} -> {
        return parser.op(hypot(::$real, ::$imag))
    }

    +op:add = (&z{OBJECT}):{OBJECT} -> {
        if(!(&z ~~ &This)) {
            throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
        }

        return parser.op(&This(::$real + &z::$real, ::$imag + &z::$imag))
    }

    +op:add = ($x{number}):{OBJECT} -> {
        return parser.op(&This(::$real + $x, ::$imag))
    }

    +op:r-add = ($x{number}):{OBJECT} -> {
        return parser.op(&This($x + ::$real, ::$imag))
    }

    +op:sub = (&z{OBJECT}):{OBJECT} -> {
        if(!(&z ~~ &This)) {
            throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
        }

        return parser.op(&This(::$real - &z::$real, ::$imag - &z::$imag))
    }

    +op:sub = ($x{number}):{OBJECT} -> {
        return parser.op(&This(::$real - $x, ::$imag))
    }

    +op:r-sub = ($x{number}):{OBJECT} -> {
        return parser.op(&This($x - ::$real, -::$imag))
    }

    +op:mul = (&z{OBJECT}):{OBJECT} -> {
        if(!(&z ~~ &This)) {
            throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
        }

        return parser.op(&This(::$real * &z::$real - ::$imag * &z::$imag, ::$real * &z::$imag + ::$imag * &z::$real))
    }

    +op:mul = ($x{number}):{OBJECT} -> {
        return parser.op(&This(::$real * $x, ::$imag * $x))
    }

    +op:r-mul = ($x{number}):{OBJECT} -> {
        return parser.op(&This($x * ::$real, $x * ::$imag))
    }

    +op:pow = (&z{OBJECT}):{OBJECT} -> {
        if(!(&z ~~ &This)) {
            throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
        }

        $r $= abs(&this)
        $theta $= ::arg()

        return parser.op(($r ** &z::$real) * ($LANG_MATH_E ** (-&z::$imag * $theta)) *\
            &This(cos(&z::$imag * loge($r) + &z::$real * $theta), sin(&z::$imag * loge($r) + &z::$real * $theta)))
    }

    +op:pow = ($x{number}):{OBJECT} -> {
        $r $= abs(&this)
        $theta $= ::arg()

        return parser.op(($r ** $x) * &This(cos($x * $theta), sin($x * $theta)))
    }

    +op:r-pow = ($x{number}):{OBJECT} -> {
        return parser.op(($x ** ::$real) * &This(cos(::$imag * loge($x)), sin(::$imag * loge($x))))
    }

    +op:div = (&z{OBJECT}):{OBJECT} -> {
        if(!(&z ~~ &This)) {
            throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
        }

        $realNumerator $= ::$real * &z::$real + ::$imag * &z::$imag
        $imagNumerator $= ::$imag * &z::$real - ::$real * &z::$imag

        $denominator $= &z::$real * &z::$real + &z::$imag * &z::$imag

        return parser.op(&This($realNumerator / $denominator, $imagNumerator / $denominator))
    }

    +op:div = ($x{number}):{OBJECT} -> {
        return parser.op(&This(::$real / $x, ::$imag / $x))
    }

    +op:r-div = ($x{number}):{OBJECT} -> {
        $realNumerator $= $x * ::$real
        $imagNumerator $= - $x * ::$imag

        $denominator $= ::$real * ::$real + ::$imag * ::$imag

        return parser.op(&This($realNumerator / $denominator, $imagNumerator / $denominator))
    }

    +op:isEquals = ($operand):{INT} -> {
        if(::$imag === 0.) {
            return parser.op(::$real == $operand)
        }

        return parser.op($operand ~~ &This && ::$real == $operand::$real && ::$imag == $operand::$imag)
    }

    +op:r-isEquals = ($operand):{INT} -> {
        if(::$imag === 0.) {
            return parser.op($operand == ::$real)
        }

        return parser.op($operand ~~ &This && $operand::$real == ::$real && $operand::$imag == ::$imag)
    }

    +op:isStrictEquals = ($operand{OBJECT}):{INT} -> {
        # Check for exact class type instead of instanceOf
        return parser.op($operand::getClass() === &This && ::$real === $operand::$real && ::$imag === $operand::$imag)
    }

    +to:text = ():{TEXT} -> {
        return parser.op(::$real ||| " + " ||| ::$imag ||| "i")
    }
}


$COMPLEX_I $= &Complex(0, 1)


function complex($real{number}, $imag{number}):{OBJECT} {
    return &Complex($real, $imag)
}

## Returns the real value of &z
function creal(&z{OBJECT}):{DOUBLE} {
    if(!(&z ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
    }

    return parser.op(&z::$real)
}

## Returns the imaginary value of &z
function cimag(&z{OBJECT}):{DOUBLE} {
    if(!(&z ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
    }

    return parser.op(&z::$imag)
}

## Returns the absolute value of &z
function cabs(&z{OBJECT}):{DOUBLE} {
    if(!(&z ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
    }

    return parser.op(abs(&z))
}

## Returns a conjugated copy of &z
function conj(&z{OBJECT}):{OBJECT} {
    if(!(&z ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
    }

    return parser.op(&z::conjugate())
}

## Returns a negated copy of &z
function cinv(&z{OBJECT}):{OBJECT} {
    if(!(&z ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&z\") must be of type \"&Complex\""
    }

    return parser.op(-&z)
}

## Returns &a + &b
function cadd(&a{OBJECT}, &b{OBJECT}):{OBJECT} {
    if(!(&a ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&a\") must be of type \"&Complex\""
    }

    if(!(&b ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 2 (\"&b\") must be of type \"&Complex\""
    }

    return parser.op(&a + &b)
}

## Returns &a - &b
function csub(&a{OBJECT}, &b{OBJECT}):{OBJECT} {
    if(!(&a ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&a\") must be of type \"&Complex\""
    }

    if(!(&b ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 2 (\"&b\") must be of type \"&Complex\""
    }

    return parser.op(&a - &b)
}

## Returns &a * &b
function cmul(&a{OBJECT}, &b{OBJECT}):{OBJECT} {
    if(!(&a ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&a\") must be of type \"&Complex\""
    }

    if(!(&b ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 2 (\"&b\") must be of type \"&Complex\""
    }

    return parser.op(&a * &b)
}

## Returns &a / &b
function cdiv(&a{OBJECT}, &b{OBJECT}):{OBJECT} {
    if(!(&a ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 1 (\"&a\") must be of type \"&Complex\""
    }

    if(!(&b ~~ &Complex)) {
        throw $LANG_ERROR_INVALID_ARGUMENTS, "Argument 2 (\"&b\") must be of type \"&Complex\""
    }

    return parser.op(&a / &b)
}
