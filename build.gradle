plugins {
    id 'java-library'

    id 'maven-publish'
    id 'signing'
}

group = 'at.jddev0.lang'
version = '1.0.0-beta-08-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_17
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'lang-interpreter'
            groupId = 'at.jddev0.lang'

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                packaging = 'jar'

                name = 'Lang Interpreter'
                description = 'The Standard Lang reference implementation of the Lang Programming language'
                url = 'https://github.com/lang-programming/lang-interpreter'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'jddev0'
                        name = 'JDDev0'
                        email = 'jddev0@dorneger.at'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/lang-programming/lang-interpreter.git'
                    developerConnection = 'scm:git:ssh://github.com:lang-programming/lang-interpreter.git'
                    url = 'https://github.com/lang-programming/lang-interpreter'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.hasProperty('publishMavenUsername')?getProperty('publishMavenUsername'):''
                password = project.hasProperty('publishMavenPassword')?getProperty('publishMavenPassword'):''
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.withType(Sign)*.enabled = project.hasProperty('publishMavenUsername')